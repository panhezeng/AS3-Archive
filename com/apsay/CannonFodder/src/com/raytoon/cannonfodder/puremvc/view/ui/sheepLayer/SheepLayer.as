///////////////////////////////////////////////////////////
//  SheepLayer.as
//  Macromedia ActionScript Implementation of the Class SheepLayer
//  Generated by Enterprise Architect
//  Created on:      14-十一月-2011 17:46:25
//  Original author: LuXianli
///////////////////////////////////////////////////////////
package com.raytoon.cannonfodder.puremvc.view.ui.sheepLayer
{
	import com.raytoon.cannonfodder.puremvc.view.ui.UIMain;
	import flash.display.BitmapData;
	import flash.display.Sprite;
	import flash.events.Event;
	import flash.system.ApplicationDomain;
	/**
	 * @author LuXianli
	 * @version 1.0
	 * @created 14-十一月-2011 17:46:25
	 */
	public class SheepLayer extends Sprite
	{
		public static const NAME:String = "SheepLayer";
		private var _runRect:Sprite;
		public function SheepLayer(){
			UIMain.setInstance(NAME, this);
		}
		
		private var _runArray:Array = [];
		private var _runWidth:int = 0;
		private var _runHeight:int = 0;
		/**
		 * 处理羊群可行走区域，并显示羊群
		 */
		public function showSheeps():void {		
			clear();
			var _sClass:Class = ApplicationDomain.currentDomain.getDefinition("HitMapPart") as Class;
			_runRect = new _sClass() as Sprite;
			_sClass = null;
			_runWidth = _runRect.width;
			_runHeight = _runRect.height;
			var _bitmapData:BitmapData = new BitmapData(_runWidth, _runHeight);
			_bitmapData.draw(_runRect);
			
			for (var y:int = 0; y < _runHeight; y ++ ) {
				
				var arr:Array = new Array();
				for (var x:int = 0; x < _runWidth; x ++ ) {
					
					if (_bitmapData.getPixel(x, y) == 0xFF0000)
						arr.push(false);
					else
						arr.push(true);
				}
				_runArray.push(arr);
				arr = null;
			}
			
			try {
				_bitmapData.dispose();
			}catch (e:*) { }
			_bitmapData = null;
			_runRect = null;
			
			createSheeps();
			addEventListener(Event.ENTER_FRAME, rendering);
		}
		
		private var _sheepArr:Array = [];
		private var _sheepX:int = 380;
		private var _sheepY:int = 350;
		/**
		 * 创建羊群
		 */
		private function createSheeps():void {
			
			var _sheepSum:int = Math.random() * 5 + 2;
			
			for (var i:int = 0; i < _sheepSum; i ++ ) {
				
				var _sheep:Sheep = new Sheep();
				_sheep.runArr = _runArray;
				addChild(_sheep);
				_sheep.showSheep();
				_sheepArr.push(_sheep);
				_sheep = null;
			}
			
		}
		/**
		 * 对羊群进行渲染
		 * @param	event
		 */
		private function rendering(event:Event):void {
			
			for each (var sheep:Sheep in _sheepArr) {
				sheep.rendering();
			}
		}
		/**
		 * 清理显示羊群
		 */
		public function clear():void {
			
			if (hasEventListener(Event.ENTER_FRAME))
				removeEventListener(Event.ENTER_FRAME, rendering);
			
			for each (var sheep:Sheep in _sheepArr) {
				
				removeChild(sheep);
				sheep.clear();
				sheep = null;
			}
			
			_sheepArr = [];
		}
		/**
		 * 绵羊的音效开关
		 * @param	value  true:开；false：关
		 */
		public function sheepSoundOnAndOff(value:Boolean):void {
			
			for each(var obj:Sheep in _sheepArr) {
				obj.soundFlag = value;
			}
		}
	}//end SheepLayer

}